cmake_minimum_required(VERSION 3.5.0)
project(cork VERSION 2.0.0 LANGUAGES C CXX)
set (CMAKE_CXX_STANDARD 20)

if(CMAKE_SOURCE_DIR STREQUAL CMAKE_CURRENT_SOURCE_DIR)
  set(is_root_project ON)
else()
  set(is_root_project OFF)
endif()

option(CORK_INSTALL "Install Cork" ${is_root_project})
option(CORK_PACKAGE "Package Cork" ${is_root_project})

include(CTest)
enable_testing()

add_compile_definitions(CORK_VERSION="${PROJECT_VERSION}")
add_compile_definitions(CORK_CODENAME="Cape Canaveral")

add_subdirectory(src)

if(CORK_INSTALL)
    install(TARGETS cork)
    if(UNIX AND NOT APPLE)
        install(FILES resources/cork.svg DESTINATION share/icons/hicolor/scalable/apps)
        install(FILES resources/roblox-player.svg DESTINATION share/icons/hicolor/scalable/apps)
        install(FILES resources/roblox-studio.svg DESTINATION share/icons/hicolor/scalable/apps)
        install(FILES distribution/roblox-player.desktop DESTINATION share/applications)
        install(FILES distribution/roblox-studio.desktop DESTINATION share/applications)
    endif()
endif()

if(CORK_PACKAGE)
    set(CPACK_COMPONENTS_ALL cork)
    set(CPACK_PROJECT_NAME ${PROJECT_NAME})
    set(CPACK_PROJECT_VERSION ${PROJECT_VERSION})
    set(CPACK_PACKAGE_NAME ${PROJECT_NAME})
    set(CPACK_PACKAGE_RELEASE 1)
    set(CPACK_PACKAGE_CONTACT "CorkHQ")
    set(CPACK_PACKAGE_VENDOR "CorkHQ")
    set(CPACK_PACKAGING_INSTALL_PREFIX ${CMAKE_INSTALL_PREFIX})
    set(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}.${CMAKE_SYSTEM_PROCESSOR}")

    include(CPack)
endif()